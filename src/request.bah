//Wrapper used for sending requests.
//Importants fields:
// .tz: timezone (number representing the offset in minutes)
// .hl: the locale language (en-US, fr-FR...)
// .geo: the geographical zone (US, FR...)
struct GT_trendRequest {
    tz: int = 0 //timezone in minutes
    hl: str = "fr" //language (en-US...)
    geo: str = "FR"
    
    kw_list: []str
    cookies: []string //session
    tokens: map:str
    requests: map:jsonElement*

    initialized: bool

    //Used for making a new session (new cookie jar)
    newSession() bool {
        req = http_request {
            timeout: 2
            url: parseUrl(GT_BASE_URL+"/?geo="+this.geo)
        }
        resp = req.send()

        if resp == null {
            println("GT: unable to fetch cookies.")
            return false
        }

        
        this.cookies = splitString(
            string(splitStringBefore(string(resp.getHeader("set-cookie")), ";")),
            "&"
        )
        return true
    }

    //Used for requesting tokens
    getTokens(tp GT_payloadTokens*) bool {
        req = http_request {
            timeout: 2
            method: "GET"
            url: parseUrl(GT_GENERAL_URL+"?hl="+this.hl+"&tz="+intToStr(this.tz)+"&req="+urlEncodePreserve(toJson(tp))+"&tz="+intToStr(this.tz))
        }
        req.setHeader("cookie", joinString(this.cookies, "; ").str())
        req.setHeader("accept", "application/json, text/plain, */*")
        req.setHeader("accept-language", this.hl)
        req.setHeader("content-type", "application/json; charset=utf-8")
        req.setHeader("Origin", "https://trends.google.com")
        req.setHeader("Alt-Used", "trends.google.com")

        resp = req.send() 
        
        if resp == null {
            println("GT: unable to fetch tokens")
            return false
        }

        j = parseJson(resp.body.str()[4:])
        
        if j == null {
            println("GT: unable to decipher tokens")
            return false
        }
        data = j.get("widgets")

        i=0; for i < len(data.children), i++ {
            id = data.children[i].get("id").str()
            this.requests[id] = data.children[i].get("request")
            tok = data.children[i].get("token").str()
            this.tokens[id] = tok
        }
        return true
    }

    //Returns interest over time chart data
    //timeframe: 'date duration-unit' where:
    //  -date:     today (str)
    //  -duration: 12    (number)
    //  -unit:     m/y/d (char)
    //Example: "today 12-d" or "today 7-d"
    interestOverTime(kw str, cat int, resolution str, timeframe str) GT_timeline* {
        if this.initialized == false {
            this.initialized = this.newSession()
            if this.initialized == false {
                println("GT: Could not fetch graph info, error while initializing GT.")
                return null
            }
        }
        gtreq = GT_payloadTokens{}
        gtreq.comparisonItem[0] = new GL_payloadComparison {
            keyword: kw
            time: timeframe
            geo: this.geo
        }
        if this.getTokens(&gtreq) == false {
            return null
        }


        gtfreq = GT_payloadOverTime{}

        oreq = this.requests["TIMESERIES"]
        
        if oreq == null {
            println("GT: Google returned empty response.")
            return null
        }
        
        oreq.scan(&gtfreq)

        req = http_request {
            timeout: 2
            method: "GET"
            url: parseUrl(GT_INT_OVER_TIME_URL+"?hl="+this.hl+"&tz="+intToStr(this.tz)+"&tz="+intToStr(this.tz)+"&req="+urlEncodePreserve(toJson(&gtfreq))+"&token="+this.tokens["TIMESERIES"])
        }

        req.setHeader("cookie", joinString(this.cookies, "; ").str())
        req.setHeader("accept", "application/json, text/plain, */*")
        req.setHeader("accept-language", this.hl)
        req.setHeader("content-type", "application/json; charset=utf-8")
        req.setHeader("Origin", "https://trends.google.com")
        req.setHeader("Alt-Used", "trends.google.com")

        resp = req.send()

        data = parseJson(resp.body.str()[5:])

        deflt = data.get("default")
        
        if deflt == null {
            println("GT: Google returned wrong response.")
            return null
        }

        r = new GT_timeline

        

        deflt.scan(r)

        return r
    }

}
